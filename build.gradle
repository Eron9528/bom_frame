group 'com.taiji'
version '1.0-SNAPSHOT'
apply plugin: 'java'
sourceCompatibility = 1.8

buildscript {
    ext {
        //定义一个变量，统一规定springboot的版本
        springBootVersion = '2.1.6.RELEASE'
        springCloudVersion = 'Finchley.SR1'
    }
    repositories {
        maven { url 'http://maven.aliyun.com/nexus/content/groups/public/' }
        jcenter()
        mavenCentral()
    }

    dependencies {//用来打包
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

allprojects {
    repositories {
        maven { url 'http://maven.aliyun.com/nexus/content/groups/public/' }
        jcenter()
        mavenCentral()
    }
    //指定编码格式
    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }
}

//设定当前模块项目中的配置
subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    //spring boot 插件
    apply plugin: 'org.springframework.boot'
    //A Gradle plugin that provides Maven-like dependency management functionality
    apply plugin: 'io.spring.dependency-management'
    dependencies {
        testCompile(
                'org.springframework.boot:spring-boot-starter-test'
        )
        //使用springboot-web组件，但是排除tomcat组件
        compile ('org.springframework.boot:spring-boot-starter-web'){
            exclude module:"spring-boot-starter-tomcat"
        }
        //使用undertow作为应用服务,作为servlet容器
        compile 'org.springframework.boot:spring-boot-starter-undertow'
        //使用健康检查组件
        compile 'org.springframework.boot:spring-boot-starter-actuator'
    }
    dependencyManagement {
        imports {
            //spring bom helps us to declare dependencies without specifying version numbers.
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }
    jar {
        manifest.attributes provider: 'gradle'
    }
}